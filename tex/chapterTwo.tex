\chapter{Ltac2}

Ltac2 is a tactic language that comes as a successor to Ltac.
Following the convention (\cite[Section 3.3.2]{thecoqdevelopmentteamCoqProofAssistant2020}, \cite{pedrotLtac2TacticalWarfare2019}) we will be calling the original language Ltac1.
In this chapter we describe main parts of Ltac2, including both semantics and the standard library.

The need for a new tactic language is well-justified due to multiple issues with Ltac1, the very first one being that it was never intended to be a full tactic language -- as per \citet{pedrotCoqHoTTminuteTickingClockwork2016}, the aim was to handle ``small automations''.
Current usages of Ltac1, i.e. Bedrock platform \cite{chlipalaMostlyautomatedVerificationLowlevel2011}, VST-Floyd~\cite{caoVSTFloydSeparationLogic2018} or Iris Proof Mode \cite{krebbersInteractiveProofsHigherorder2017, krebbersMoSeLGeneralExtensible2018} go far beyond what one would consider ``simple''.
This discrepancy between intended purpose and usage becomes apparent through unpredictability and lack of uniformity in semantics and issues with maintainability (\cite[Section 3.3.2]{thecoqdevelopmentteamCoqProofAssistant2020}, \cite{pedrotLtac2TacticalWarfare2019}).

The Coq community developed multiple alternatives to Ltac1, such as Mtac \cite*{zilianiMtacMonadTyped2013, kaiserMtac2TypedTactics2018a}, which focuses on stronger types for tactics,
Rtac \cite{malechaExtensibleEfficientAutomation2016} and Cybele \cite{claretLightweightProofReflection2013} both providing reflection-based tactics, and others.
Ltac2, on the other hand, does not aim for a new approach to tactics, but rather to be a direct successor to Ltac1, being backwards-compatible where possible, but maintaining reasonable semantics.
It is still experimental and was integrated in Coq in version 8.11, so the description here applies only to versions 8.11 and 8.12, which are available at the moment.

In this chapter we first present a suite of examples which give a taste of what Ltac2 programs look like, building up to a simple first-order logic solver tactic.
Then we present details of the semantics of Ltac2 in a more formal way.
This chapter assumes some knowledge of Ltac1 from the reader.

\section{Ltac2 by example}
\label{sec:ltac2-by-examples}

Ltac2 is an ML-family language, which means it is a call-by-value, effectful functional language.
In particular, it has:
\begin{itemize}
\item static types, including algebraic datatypes, rank-1 polymorphism, and several built-in types (\coqe{bool}, \coqe{int}, \coqe{string}, \coqe{constr}, \coqe{ident}, \ldots).
  \begin{coq}
  (*lists of integers *)
  Ltac2 Type rec int_list := [IntNil | IntCons (int, int_list)].
  (* polymorphic sum type *)
  Ltac2 Type ('a, 'b) sum := [Left ('a) | Right ('b)].
  \end{coq}
\item (lambda) functions, and pattern-matching:
  \begin{coq}
  (* function definition *)
  Ltac2 add_one (a : int) := Int.add a 1.
  (* binding lambda-function to a name *)
  Ltac2 rec all_gt_zero := fun (v : int_list) =>
    match v with
    | IntNil => true
    | IntCons h t => and (Int.gt h 0) (all_gt_zero t)
    end.
  \end{coq}
\end{itemize}

Most of it, however, is conventional for ML-family languages and  we are interested in Ltac2 because it has more than this: the ability to manipulate the proof state.

\subsection{Primitive tactics}
\label{sec:primitive-tactics}

First of all, Ltac2 reflects Coq terms into \coqe{constr} datatype and provides a library to manipulate them.

\begin{coq}
Ltac2 two () := constr:(S (S Z)).
Ltac2 three () := '(3).
Ltac2 untrue () := match (Constr.equal (two ()) (three ())) with
  | true => print (of_string "two is equal to three")
  | false => print (of_string "two is not equal to three")
  end.
\end{coq}

In order to ensure sound behavior with respect to the proof state, the body of an Ltac2 definition is required to be either a function, a constant, a pure constructor recursively applied to values or a (non-recursive) let binding of a value in a value.
This is the reason why we thunk definitions in the example above.

Ltac2 also features a quotation mechanism.
In the example above \coqe{two} is a thunked Coq term, while \coqe{three} is a thunked \coqe{open_constr}, which can contain existential variables.

Ltac2 doesn't expose Ltac1 tactics directly inside, but rather reimplements them from scratch and OCaml primitives.
This provides great flexibility for introducing changes to them, but also means that Ltac2 isn't compatible with other tactic languages, like SSReflect~\cite[Section~3.1.3]{thecoqdevelopmentteamCoqProofAssistant2020}.

\begin{coq}
Ltac2 print_goal () := print (concat (of_string "Current goal is: ")
                                     (of_constr (Control.goal ())).
Ltac2 split_assumption () := repeat (first [split | assumption]).
\end{coq}

\subsection{Matching terms and goals}
\label{sec:matching-terms}

In order to work with Coq terms effectively, Ltac2 gives the user the ability to match them in a way that is very similar syntactically to matching proper Ltac2 terms: the user has to swap \coqe{match} for \coqe{match!} and prepend variable names bound in patterns with question marks:
\begin{coq}
Ltac2 is_conjunction (v : constr) := match! v with
  | ?p /\ ?q => true
  | _ => false
  end.
\end{coq}

Ltac2 also recreates goal-matching tacticals:
\begin{coq}
Ltac2 one_conjunct_trivial :=
  match! goal with
  | [ h : ?p |- ?p /\ ?q] => split > [assumption | ()]
  | [ h : ?q |- ?p /\ ?q] => split > [() | assumption]
  | _ => ()
  end.
\end{coq}

The syntax is the same as for Ltac1.
Units \coqe{()} are no-op in the snippet above, but we include them for readability.
The other new element of syntax is
\begin{coq}
tactic1 > [tactic2_1 | $\ldots$ | tactic2_n]
\end{coq}
which stands for tactic dispatch and was written as
\begin{coq}
tactic1; [tactic2_1 |  $\ldots$ | tactic2_n]
\end{coq}
in Ltac1.

Notably, Ltac2 not only provides machinery for pattern-matching terms and goals, but also exposes notations and internal functions for them.
This is one of the key elements that allowed us to implement pattern-matching for MoSeL which we describe in chapter~\ref{chap:imatch}.

\subsection{Notations and scopes}
\label{subsec:ltac2-notations-scopes}

Since Ltac2 makes a clear distinction between Coq terms and Ltac2 terms, it has to rely on notations to recreate automagic parsing of Ltac1.

There are two different categories of notation machinery in Ltac2: notations and scopes.
Notations are user-defined and look very similar to Coq \coqe{Notation} command, while scopes are custom parsers for arguments.
\begin{coq}
Ltac2 Notation "is_conjunction_pretty" v(constr) := is_conjunction v.
(* without notation *)
Ltac2 check1 () := is_conjunction constr:(True /\ False).
(* with notation *)
Ltac2 check2 () := is_conjunction_pretty (True /\ False).
\end{coq}

In this case, scope annotation \(v(constr)\) says that argument \coqe{v} should be parsed as a Coq term.
Currently only a limited number of pre-defined scopes is available and there is no way to extend them without changing Ltac2 grammar in OCaml.
Some of the available scopes are \coqe{cosntr}, \coqe{ident}, \coqe{list0}.
The former two allow parsing of respective types instead of explicit quotations, while the latter is combinator that takes a scope \coqe{e} and produces a parser for list of \coqe{e}s.
Notations, unlike in Coq, are parse-only and are mostly used to define better-looking aliases for existing Ltac2 tactics.

\subsection{Exceptions}
\label{subsec:ltac2-exceptions}

Ltac2 features an extensible exception type, while in Ltac1 one could only specify a number of the backtracking points to jump through.
\begin{coq}
Ltac2 Type exn ::= [ MyNewException(string) ].
\end{coq}

These exceptions can either be thrown as fatal (\coqe{throw e}) or as recoverable errors (\coqe{zero e}).
Recoverable errors are part of backtracking system in Ltac2.
We are going to cover it in detail in section~\ref{sec:semantics-ltac2}, but for now it suffices to say that there are three primitives, which are in the \coqe{Control} library:

\begin{coq}
Ltac2 Type 'a result := [ Val ('a) | Err (exn) ].

val zero : exn -> 'a
val plus : (unit -> 'a) -> (exn -> 'a) -> 'a
val case : (unit -> 'a) -> ('a * (exn -> 'a)) result
\end{coq}

The primitives perform the following actions: \coqe{zero} throws an exception, \coqe{plus} introduces a backtracking point, which will switch to the second branch if the first argument throws an exception.
And finally, \coqe{case} matches backtracking values.
It tries to evaluate the ``leftmost'' backtracking value from the first argument and return it in \coqe{Val} constructor, with the rest of the backtracking value.
If the ``leftmost'' value turns out to be an error, \coqe{case} returns the exception wrapped in the \coqe{Exn} constructor.

\begin{coq}
Ltac2 fatal_exn () := throw (MyNewException "fatal exn").
Ltac2 regular_exn () := zero (MyNewException "thrown exn").
Ltac2 catches_exn () := match case (regular_exn) with
| Err e => match e with
  | MyNewException s => print (of_string s)
  | _ => zero e
  end
| Val ans => let (x, k) := ans in
  plus (fun _ => x) k
end
\end{coq}

\subsection{Propositional solver}
\label{sec:propositional-solver}

Having covered the essentials of the language, we provide a simple tautology solver for first-order logic akin to one described by \citet{zilianiMtacMonadTyped2013} as an example.

We define a recursive function \coqe{tauto} which matches the current goal with a set of patterns and then applies fitting tactics.
While we are not passing the goal explicitly, \coqe{tauto} is called on each generated subgoal via recursion.

\begin{figure}[H]
\begin{coq}
Ltac2 rec tauto () :=
  match! goal with
  | [|- True] =>
    refine '(I)
  | [|- ?p /\ ?q] =>
    split > [tauto ()| tauto ()]
  | [|- ?p \/ ?q] =>
    (* backtracking branching *)
    plus (fun _ => refine '(or_introl _); tauto ())
         (fun _ => refine '(or_intror _); tauto ())
  | [|- forall _, _] =>
    intros ?; tauto ()
  | [|- exists _ , _] =>
    eexists; tauto ()
  | [|- _] => ltac1:(eassumption)
  end.
\end{coq}
\caption{solver for first-order logic}
\label{fig:ltac2-solver}
\end{figure}

There are two interesting points in the snippet above:
\begin{itemize}
\item We are using a backtracking primitive \coqe{plus} to attempt proving both left and right branches.
  And while the second argument to \coqe{plus} takes the exception generated by the first argument, we simply ignore it, since we do not introduce any recoverable errors in the solver.
\item The second one is availability of some of the usual tactics.
  While one can not use any other tactic language in combination with Ltac2, we see many of the usual tactics, which have been recreated inside Ltac2, e.g.  \coqe{refine}, \coqe{split}, \coqe{eexists}.
  Some other tactics are not present though, like \coqe{eassumption}, but with quotations we can still use it from ltac1.
\end{itemize}

\section{Semantics of Ltac2}
\label{sec:semantics-ltac2}

As we said in the very beginning, Ltac2 has effectful computations, which are interpreted inside a tactic monad.
The monad is provided by Coq's new tactics engine \cite{spiwackAbstractTypeConstructing2010}.
In a way, we can see Ltac2 as a DSL over the engine, coupled with proof-state manipulation primitives and notations.

In this chapter we explain Ltac2 from precisely this point of view.
The tactic engine itself is also explained in a blog post by \citet{pedrotCoqHoTTminuteTickingClockwork2016}, but here we try to expand on it, explaining not only how Ltac2 interfaces the monad, but also how some user-facing tactics are implemented.

Note that we focus on the monad from a user-perspective, so the types provided in ML syntax should only be seen as guidance for the Ltac2 user, and not as ones true to the implementation.

\subsection{Tactics form a monad}
\label{sec:monad-tactics}

When the user applies a tactic to a goal in Coq, they expect to get back a list of new goals generated or an error:
\begin{ocaml}
type 'a tactic = goal * state -> (exn, goal list * state * a) sum
\end{ocaml}
where \ocamle{state} encapsulates proof state, such as existential variable instantiations.
The \coqe{tactic} type is a monad (based on LCF~\cite{gordonEdinburghLCFMechanised1979}) and it comes equipped with usual monad operators \ocamle{return} and \ocamle{bind}:
\begin{ocaml}
val tclReturn : 'a -> 'a tactic.
val tclBind : 'a tactic -> ('a -> 'b tactic) -> 'b tactic.
\end{ocaml}

The functions above are not exposed to the user, but rather used while interpreting Ltac2 programs.
the user can think of Ltac2 as a DSL over \coqe{tactic} monad with do-notations.

This already gives us sequencing inside the language: \coqe{tactic1; tactic2} and ability to write simple programs that trasnform goals.
Technically we could also implement non-backtracking branching, but this would require OCaml implementation instead of reliance on primitive operations inside the monad, so we postpone it until later.

We can already explain some of the primitive tactics, like \coqe{Control.goal}, which returns the current goal as a term.
\begin{coq}
  Ltac2 goal : unit -> constr.
\end{coq}\vspace{-1em}
Morally, \coqe{goal} is implemented in OCaml as \ocamle{fun (g : goal) -> Right [g] g}.

\subsection{Backtracking monad for tactics}
\label{sec:backtr-monad-tact}

The monad described above suffices for many purposes, but it doesn't allow backtracking naturally.
The usual solution for this is to apply a \ocamle{ListT} transformer to the monad.
However, there is a well-known issue with ListT~\cite{jones1993composing} -- in general the monad it generates does not satisfy associativity laws, and hence, is not a monad at all.
So, instead of applying \ocamle{ListT}, we interleave the exception reading effect with return value generation and get the following type:
\begin{ocaml}
type 'a elist = Node of (exn -> 'a enode)
and 'a enode = Zero of exn | Plus of 'a * 'a elist

type +'a tactic = goal * state-> (goal list * state * 'a) elist
\end{ocaml}
This type was derived by \citet{spiwackAbstractTypeConstructing2010} using the construction by \citet{kiselyovBacktrackingInterleavingTerminating2005} and provides several primitives.
We are interested in the following three:
\begin{ocaml}
val tclZERO : exn -> 'a tactic
val tclPLUS : 'a tactic -> (exn -> 'a tactic) -> 'a tactic
type 'a case =
  | Fail of exn
  | Next of 'a * (exn -> 'a tactic)
val tclCASE : 'a tactic -> 'a case tactic
\end{ocaml}

The type signature for the monad and the interface are not the easiest to grasp or to form an intuition about, so we are going to show several examples on how they apply to real-world tactics.
The important difference to the previous monad primitives is that these are exposed inside Ltac2 and are available to the user in the \coqe{Control} module.
These primitives come not on their own but with several laws that guard evaluation.
In figures \ref{fig:zero_plus_eq}, \ref{fig:case_eq}, \ref{fig:let_eq}, \coqe{zero}, \coqe{plus} and \coqe{case} correspond to their respective functions and \coqe{t, u, f, g, e} are values.
\begin{figure}[H]
\begin{coq}
plus t zero $\equiv$ t ()
plus (fun () => zero e) f $\equiv$ f e
plus (plus t f) g $\equiv$ plus t (fun e => plus (f e) g)
\end{coq}
\caption{Equations concerning \coqe{zero} and \coqe{plus}}
\label{fig:zero_plus_eq}
\end{figure}

One can think about \coqe{plus} as a \coqe{cons} constructor of the list of results and about \coqe{zero} as a \coqe{nil} constructor, used when a tactic couldn't return any result and thus failed.

Contrary to the previous time, the exposure of primitives gives us an ability to recreate branching tactics inside Ltac2.
Somewhat surprisingly, backtracking branching is simpler that the non-backtracking one, so we first present the former:
\begin{coq}
tactic1 + tactic 2 := plus (tactic1) (fun _ => tactic2)
\end{coq}
It works in the following way: if \coqe{tactic1} evaluates to an exception, that is, a \coqe{zero e}, it is discarded as per the second equation of~\ref{fig:zero_plus_eq}.
If it evaluates to a list of results, they are chained with those from \coqe{tactic2} following the third law.
Both this example and the last equation in figure~\ref{fig:zero_plus_eq} somewhat break the intuition of \coqe{plus} being a \coqe{cons} constructor and instead behaves more like the \coqe{app} operation on lists of results.

Now let's turn to the last element of the interface -- \coqe{case}.
Intuitively, it evaluates the thunk provided and then takes it apart, serving as a pattern-matching construction for the lists of \coqe{plus} and \coqe{zero}.
\begin{figure}[H]
\begin{coq}
case (fun () => zero e) $\equiv$ Left e
case (fun () => plus (fun () => t) f) $\equiv$ Right (t,f)
\end{coq}
\caption{Equations concerning \coqe{case}}
\label{fig:case_eq}
\end{figure}

This enables introspection of results.
In particular, we can implement a function, that cuts off backtracking and returns the first value, if there is one:
\begin{coq}
Ltac2 once t := match (case t) with
  | Err exn => zero exn
  | Val v => let (x, rr) := v in x
  end.
\end{coq}

And using \coqe{once}, we can also implement non-backtracking branching:
\begin{coq}
tactic1 ||tactic2 := once (tactic1 + tactic2).
\end{coq}

Finally, there are two laws guarding the behavior of binding with respect to \coqe{plus} and \coqe{zero}.
\begin{figure}[H]
\begin{coq}
(let x := zero e in u) $\equiv$ zero e
(let x := plus t f in u) $\equiv$ (plus (fun () => let x := t in u)
                                 (fun e => let x := f e in u))
\end{coq}
\caption{Equations concerning binders}
\label{fig:let_eq}
\end{figure}

The first equation states that the exceptions propagated through any binding and the second one describes propagation of the backtracking values very much alike \ocamle{ListT}.
An important thing to keep in mind is that the backtracking interface above comes \emph{in addition} to the monadic interface, which means the user can ignore these functions as long as backtracking isn't of concern.

\section{The rest of the language}
The main point of this chapter is that Ltac2 can be seen as a substitute for Ltac1, while keeping stricter informal semantics.
Of course, there is more to it than we could describe here, so we refer interested readers to the documentation \cite[Section 3.3.2]{thecoqdevelopmentteamCoqProofAssistant2020}.

As the language is still in development, we are going to share our experience of using it in the remaining chapters to try to help the development.

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "thesis"
%%% TeX-parse-self: t
%%% TeX-auto-save: t
%%% reftex-cite-format: natbib
%%% reftex-default-bibliography: ("/home/buzzer/my-dir/ed/uni/saar/prjcts/iris/npm/tex/TacticsProofs.bib")
%%% End: