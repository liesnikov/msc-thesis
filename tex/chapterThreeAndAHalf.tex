\chapter{Reimplementing MoSeL in Ltac2}
\label{chap:ltac2-tactics-mosel}

In this chapter we present our reimplementation of MoSeL from Ltac1 to Ltac2.
As evident from figure~\ref{fig:ipm-diagram}, we only have to alter the topmost layer of the implementation -- the rest will stay exactly the same.
In the reimplementation we follow both the ideas and the design of MoSeL closely to ensure backwards compatibility and correct usage of existing infrastructure.

We are going to describe some details from the new Ltac2 part of MoSeL and then share our experiences with translation of existing Ltac1 code to Ltac2.

\section{Ltac2 layer of MoSeL}

The translation is mostly mechanical and serves as a platform for features developed in the following chapters.
Namely, for a basic comparison we recreate a set of basic tactics to introduce and destruct BI connectives, like wand, conjunction, disjunction, existentials.
We limit ourselves to this small subset, since in chapter~\ref{chap:postponed_splitting} we recreate a much larger set.
There we essentially omitting only tactics with intro-patterns and inputs that require complicated parsing.
However, the value of this chapter lies in the comparison of Ltac1 and Ltac2 implementation and we summarize our experiences taking into account the whole development and not only the basic MoSeL proof mode reimplementation.

For the translation of Ltac1 tactics to Ltac2 the difference is mainly syntactical, so we are going to simply show two examples.
We should note that we also switch from camel case (\coqe{iExact}) naming convention to snake case (\coqe{i_exact}) for Ltac2 tactics.
This is done so that we can later introduce aliases that do automatic parsing, as MoSeL users expect them to, and still utilize original camel case identifiers.

As for the examples, first we are going to consider \coqe{i_exact_spatial}, for which we have a comparison in the previous chapter (figure~\ref{fig:ltac1-iassumption}).
And then a simplified implementation of \coqe{i_assumption} in Ltac2.

As Ltac2 replicates many of Ltac1 tactics, we can simply swap tactics for their Ltac2 counterparts.
In the \coqe{i_exact} the most important changes are the fact that internal tactics (\coqe{iSolveTC} and others) became Ltac2 functions, that now require an explicit call to be invoked and a different notation for goal dispatch.
We also have to use a different notation for Coq terms construction.
\begin{figure}[H]
\begin{coq}
Ltac2 i_exact_spatial (j : constr) (p: constr) :=
  refine '(tac_assumption _ \$j false \$p _ _ _ _) >
           [ pm_reflexivity ()
           | i_solve_tc ()
           | pm_reduce (); i_solve_tc ()]
\end{coq}
  \caption{Ltac2 implementation of iExactSpatial}
  \label{fig:iexact-ltac2}
\end{figure}
We also switch to \coqe{refine} instead of \coqe{eapply} due to small irrelevant differences in semantics of the tactics.

For comparison, we provide the Ltac1 definition from figure~\ref{fig:ltac1-iassumption} again:
\begin{coq}
Tactic Notation "iExactSpatial" constr(H) constr(P) :=
  eapply (tac_assumption _ j false P);
    [pm_reflexivity
    |iSolveTC
    |pm_reduce; iSolveTC]
\end{coq}

For \coqe{iAssumption} the idea is that the user doesn't provide explicit identifier of the resource they have in mind.
Instead, the tactic is supposed to find it on its own.

Simply asserting the existence of such an identifier in a Coq lemma doesn't solve anything, since we clearly can not prove existence of the relevant resource in general.
Hence the universally quantified identifier in the definition of the Coq lemma~\ref{fig:tac-assumption}.
To solve the problem with providing the identifier for lemma we go through all elements of the environment in an Ltac2 function and try to apply the lemma with each one.

\begin{figure}
\begin{coq}
Ltac2 i_assumption () :=
  let rec find (p : coq_bool) (g : ipm_env) (q : ipm_prop) :=
      lazy_match! g with
      | Esnoc ?gg ?j ?pp =>
        first [ refine '(tac_assumption _ \$j \$p \$pp _ _ _ _) >
                [ pm_reflexivity ()
                | i_solve_tc ()
                | pm_reduce (); i_solve_tc ()]
              | find p gg q]
      end
  in
  lazy_match! goal with
  | [|- envs_entails (Envs ?gp ?gs _) ?q] =>
     first [ find '(true) gp q
           | find '(false) gs q
           | i_assumption_coq ()
           | Control.zero (Iriception (of_string "no assumption matching " ++
                                       of_constr q ++
                                       of_string " was found"))]
  end.
\end{coq}
\caption{Simplified \coqe{i_assumption} definition in Ltac2}
\label{fig:i-assumption-def}
\end{figure}



The listing is in figure~\ref{fig:i-assumption-def}.
Since there are two contexts to go through, we factor out recursion into a \coqe{find} function, which takes a Boolean flag \coqe{p} to tell apart spatial and intuitionistic context, a context \coqe{g} and a proposition \coqe{q}.
We then use exactly the same code as in \coqe{i_exact}, except this time the choice of the context and proposition is done mechanically by the \coqe{find} function.

We also put Ltac2's ability to define type aliases to use -- \coqe{coq_bool}, \coqe{ipm_env}, and \coqe{ipm_prop} are all simply Coq terms -- \coqe{constr}s.
However, we believe and it is common knowledge\footnote{\href{https://en.wikibooks.org/wiki/Yet\_Another\_Haskell\_Tutorial/Type\_advanced}{en.wikibooks.org/wiki/Yet\_Another\_Haskell\_Tutorial/Type\_advanced}} that they improve readability.

The only part which wasn't described so far is \coqe{i_assumption_coq ()}.
This is an example of tactics composition -- \coqe{i_assumption_coq} is applying a different lemma from \coqe{tac_assumption}, which tries to find an assumption of shape \(\Vdash Q\) in the Coq context instead of MoSeL context.

Finally, we try to search in the intuitionistic (\coqe{find '(true) gp q}), the spatial (\coqe{find '(false) gs q}) and the Coq contexts sequentially.
If neither of the searches yields any results we throw an Iris exception (\coqe{Iriception}) with a message on what couldn't be found in the context.

The differences from Ltac1 implementation are again just syntactical and don't contain anything of interest except things already discussed for \coqe{iExactSpatial}, so we don't bore the reader with the direct comparison.

\section{The examplary proof}
\label{sec:examplary-proof-in-ltac2-mosel}

We now show what the proof looks like in the reimplemented version of MoSeL.

The full listing is in figure~\ref{fig:example-proof-mosel-ltac2}.
Since we didn't touch environment or rendering of the goals, for the user only tactics changed and everything else stayed the same, including the proof states.

Perhaps, the biggest difference from the Ltac1 version is the lack of useful notations and intropatterns.
However, this is not Ltac2's fault, but rather ours -- we didn't focus on them and this aspect can still be improved.
In particular, now we have to destruct the introduced separating conjunction with two tactic invocations: what previously was just \coqe{iIntros "[HP H]".} now becomes manual introduction of the separating conjunction \coqe{i_intro_named "HS".} and then its destruction \coqe{i_and_destruct '(INamed "HS") '(INamed "HP") '(INamed "H").}
Same goes for the existential, while previously we could destruct both the existential and disjunction in one command, now we have to perform two.

The reason why didn't focus on them was because it is impossible currently to extend proper intropatterns (see section~\ref{sec:desir-feat-ltac2-chap4}).
However, it is our fault to an extent, since we could've gone the same root as the original MoSeL implementation, where the intropatterns are Coq strings, which get parsed in Gallina functions.
However, the full reimplementation of MoSeL, again, isn't the focus of this thesis.

\begin{figure}
\begin{coq}
Lemma example {A : Type} (P : PROP) (Phi Psi : A â†’ PROP) :
  P * (exists a, (Phi a) \/ (Psi a)) -* exists a, (P * Phi a) \/ (P * Psi a).
Proof.
  (* iIntros "[HP H]". *)
  i_intro_named "HS".
  i_and_destruct '(INamed "HS") '(INamed "HP") '(INamed "H").
  (* iDestruct "H" as (x) "[H1|H2]". *)
  i_exist_destruct '(INamed "H") as x '(INamed "HD").
  i_or_destruct '(INamed "HD") '(INamed "H1") '(INamed "H2").
  - i_exists$\text{~}$x. i_left (). i_split_l ["HP"] ;; i_assumption ().
  - i_exists$\text{~}$x. i_right (). i_split_l ["HP"] ;; i_assumption ().
Qed.
\end{coq}
\caption{An example of a proof using Ltac2 version of MoSeL}
\label{fig:example-proof-mosel-ltac2}
\end{figure}


\section{Potential and observed improvements from translation to Ltac2}
\label{sec:impr-from-transl}

While translating implementation from Ltac1 to Ltac2, we encountered several points where Ltac2 was particularly useful.

\paragraph{Fix arities from original implementation}

In the original implementation there aren't tactics of arbitrary arity.
Instead, due to Ltac limitations, developers were forced to define several variants of tactics, each with fixed arity.

\begin{coq}
Tactic Notation "iExists" uconstr(x1) "," uconstr(x2) :=
  iExists x1; iExists x2.
Tactic Notation "iExists" uconstr(x1) "," uconstr(x2) "," uconstr(x3) :=
  iExists x1; iExists x2, x3.
$\ldots$
\end{coq}

With Ltac2 we can use scopes for lists, which allow arbitrary arities.
The example above would look as follows:

\begin{coq}
  Ltac2 Notation "iExists" lc(list1(thunk(constr), ",")) :=
  i_exists$\text{~}$lc.
\end{coq}

The scope parses a list of Coq terms (\coqe{constr}) separated by commas into a list \coqe{lc}.
Then \coqe{i_exists} iterates over the list and applies \coqe{iExists} to each element.

\paragraph{Error messages}

Another pain point of Ltac development is lack of proper error handling.
With Ltac2 there two features that can alleviate the problem -- we can define our own custom error classes and there is a proper error handling mechanism, which allows us to match on the error thrown.

As with the \coqe{i_assumption} above, there are multiple instances of nesting tactics within each other.
One way to improve error message thrown by iAssumption would be to catch potential error thrown by \coqe{i_assumption_coq} and append it to the error message produced in \coqe{i_assumption}, so that user can see why the tactic failed.

\section{Desirable features of Ltac2}
\label{sec:desir-feat-ltac2-chap4}

However, there also were several features that we wished Ltac2 had and that impacted the scope of potential improvements.

\paragraph{User-defined scopes}

Perhaps, one of the useful features of the original MoSeL implementation that is missing from our implementation is intropatterns.
The proper way to define them with Ltac2 would be to extend datatype for intropattern with MoSeL-specific symbols and then parse it with a custom scope.

Unfortunately, the latter isn't currently possible without extending OCaml implementation of Ltac2, either via forking Coq or using an Ocaml plugin that modifies Ltac2 grammar.

\paragraph{User-defined pretty-printing}
Another missing feature would be lack of user-defined printing rules.
At the moment this mostly impacts error printing, since when an exception is thrown, it gets printed using the default Ltac2 pretty-printer.
And it would be good to have the ability to control the display of exceptions, or Ltac2 data types in general.

\paragraph{notypeclasses refine}

Ltac2 also currently lacks a variant of \coqe{refine}, which doesn't resolve type classes on the application.
This tactic is heavily used in MoSeL development.

\paragraph{Interoperability between Ltac1 and Ltac2}

While the goal of the rewriting was to give platform for further experiments, some of the tactics were easier to import, like \coqe{iFresh} or various small helper tactics.
And while there is a way to call Ltac1 tactics from Ltac2 code, there is no proper way to pass return values there or back.
E.g.\ \href{https://gitlab.mpi-sws.org/iris/string-ident/}{iris string-ident} has to pass terms through the goal to get the result from one to another.

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "thesis"
%%% TeX-parse-self: t
%%% TeX-auto-save: t
%%% reftex-cite-format: natbib
%%% reftex-default-bibliography: ("/home/buzzer/my-dir/ed/uni/saar/prjcts/iris/npm/tex/TacticsProofs.bib")
%%% End: